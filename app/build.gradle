apply plugin: 'com.android.application'

android {
    def versionPropsFile = file('version.prop')
    Properties versionProps = new Properties()
    if (versionPropsFile.exists())
        versionProps.load(new FileInputStream(versionPropsFile))

    def _versionCode = (versionProps['VERSION_CODE'] ?: "1").toInteger()
    def _major = (versionProps['VERSION_MAJOR'] ?: "1").toInteger()
    def _minor = (versionProps['VERSION_MINOR'] ?: "0").toInteger()
    def _patch = (versionProps['PATCH'] ?: "0").toInteger() + 1

    List<String> runTasks = gradle.startParameter.getTaskNames()
    for (String item : runTasks) {
        if (item.contains("assembleRelease")) {
            _versionCode = _versionCode + 1
        }
        if (item.contains("clean")) {
            _minor = _minor + 1
            _patch = -1
        }
    }

    if (_minor == 10) {
        _minor = 0
        _major = _major + 1
    }

    versionProps['VERSION_CODE'] = _versionCode.toString()
    versionProps['VERSION_MAJOR'] = _major.toString()
    versionProps['VERSION_MINOR'] = _minor.toString()
    versionProps['PATCH'] = _patch.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "net.harimurti.joylive"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode _versionCode
        versionName "${_major}.${_minor}.${_patch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${defaultConfig.applicationId}_v${defaultConfig.versionName}.apk"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:animated-vector-drawable:28.0.0'
    implementation 'com.android.support:support-media-compat:28.0.0'
    implementation 'com.android.support:exifinterface:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.squareup.okhttp3:okhttp:3.14.1'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.google.android.exoplayer:exoplayer:2.9.0'
    implementation 'com.google.android.exoplayer:extension-rtmp:2.9.0'
    implementation 'de.hdodenhof:circleimageview:3.0.0'
    implementation 'com.github.ybq:Android-SpinKit:1.2.0'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
